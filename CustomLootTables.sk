#= Adding rare items to loot tables
on right click on chest:
	# Conditions
	isASurvivalWorld(player's world) is true
	set {_table} to tag "LootTable" of nbt of clicked block
	{_table} is set
	wait 1 tick
	
	# Grab all free slots within the chest
	loop integers from 0 to 26:
		slot loop-number of inventory of clicked block is air
		add loop-number to {_slots::*}
	
	size of {_slots::*} > 0
	
	# Grab chest tag and cleanse it
	replace all "minecraft:chests/" in {_table} with ""
	set {_slot} to random element out of {_slots::*}

	# Perform switch on cleansed table name
	switch "%{_table}%":
		# Nether fortresses
		case "nether_bridge":
			chance of 2.5%:
				set slot {_slot} of clicked block to {item.WitheredHeart}

		# Woodland mansion
		case "woodland_mansion":
			chance of 2.5%:
				set slot {_slot} of clicked block to totem of undying
				remove {_slot} from {_slots::*}
			loop random integer between 0 and 3 times:
				set {_slot} to random element out of {_slots::*}
				evaluate "set slot {_slot} of clicked block to %random integer between 2 and 7% emeralds"
				remove {_slot} from {_slots::*}

		# Igloo chests: always have a Frost Walker book within
		case "igloo_chest":
			chance of 100%:
				set slot {_slot} of inventory of clicked block to enchanted book with nbt "{StoredEnchantments:[0:{lvl:%random integer between 1 and 2%s,id:9s}]}"


#= Adding special drops to minibosses
on death of evoker:
	isASurvivalWorld(victim's world) is true
	drop 1 of {item.DustyEmerald} at the victim
on death of elder guardian:
	isASurvivalWorld(victim's world) is true
	drop 1 of {item.TwinklingSeaCrystals} at the victim
on death of wither:
	isASurvivalWorld(victim's world) is true
	set {_loc} to victim's location
	wait 0.5 seconds
	drop 1 of {item.WitheredHeart} at {_loc}


#= Dusty Emeralds were updated at one point with a new lore
# (They're now converted to totems at the Valor Shop instead of via crafting)
# This lets people convert them to the new ones for selling
on right click:
	player's tool is {item.DustyEmeraldOld}
	set player's tool to (amount of player's tool) of {item.DustyEmerald}
